buildscript {

  ext.buildConfig = [
      compileSdk: 28,
      minSdk    : 21,
      targetSdk : 27,
      version   : [
          code: 3060301,
          name: '4.0.0-nightly'
      ]
  ]

  ext.versions = [
      androidPlugin      : "3.2.0-rc03",
      conductor          : "2.1.5",
      dagger             : "2.16",
      exoPlayer          : "2.8.4",
      exoPlayerExtensions: "2.7.0-1",
      coroutines         : "0.26.1",
      kotlin             : "1.2.70",
      room               : "2.0.0-rc01",
      androidX           : "1.0.0"
  ]

  ext.deps = [
      AndroidX            : [
          supportAnnotations: "androidx.annotation:annotation:${versions.androidX}",
          appCompat         : "androidx.appcompat:appcompat:${versions.androidX}",
          constraintLayout  : "androidx.constraintlayout:constraintlayout:1.1.3",
          recyclerView      : "androidx.recyclerview:recyclerview:${versions.androidX}",
          transitions       : "androidx.transition:transition:${versions.androidX}",
          palette           : "androidx.palette:palette:${versions.androidX}",
          testRunner        : "androidx.test:runner:1.1.0-alpha4",
          mediaCompat       : "androidx.media:media:${versions.androidX}",
          fragment          : "androidx.fragment:fragment:${versions.androidX}",
          ktx               : "androidx.core:core-ktx:${versions.androidX}",
          Room              : [
              runtime : "androidx.room:room-runtime:${versions.room}",
              compiler: "androidx.room:room-compiler:${versions.room}",
              testing : "androidx.room:room-testing:${versions.room}",
              rxJava  : "androidx.room:room-rxjava2:${versions.room}"
          ]
      ],

      androidGradlePlugin : "com.android.tools.build:gradle:${versions.androidPlugin}",
      material              : "com.google.android.material:material:${versions.androidX}",
      floatingActionButton: "com.getbase:floatingactionbutton:1.10.1",
      materialDialogs     : "com.afollestad.material-dialogs:core:0.9.6.0",
      materialCab         : "com.afollestad:material-cab:1.2.0",
      picasso             : "com.squareup.picasso:picasso:2.71828",
      tapTarget           : "com.getkeepsafe.taptargetview:taptargetview:1.12.0",
      chapterReader       : "com.github.PaulWoitaschek:ChapterReader:0.1.4",

      Conductor           : [
          base     : "com.bluelinelabs:conductor:${versions.conductor}",
          lifecycle: "com.bluelinelabs:conductor-rxlifecycle2:${versions.conductor}",
          support  : "com.bluelinelabs:conductor-support:${versions.conductor}"
      ],

      crashlytics         : "com.crashlytics.sdk.android:crashlytics:2.9.5@aar",
      fabricGradlePlugin  : "io.fabric.tools:gradle:1.26.0",

      Dagger              : [
          core    : "com.google.dagger:dagger:${versions.dagger}",
          compiler: "com.google.dagger:dagger-compiler:${versions.dagger}"
      ],

      ExoPlayer           : [
          core: "com.google.android.exoplayer:exoplayer-core:${versions.exoPlayer}",
          opus: "com.github.PaulWoitaschek.ExoPlayer-Extensions:opus:${versions.exoPlayerExtensions}",
          flac: "com.github.PaulWoitaschek.ExoPlayer-Extensions:flac:${versions.exoPlayerExtensions}"
      ],

      moshi               : "com.squareup.moshi:moshi:1.6.0",
      rxAndroid           : "io.reactivex.rxjava2:rxandroid:2.1.0",
      rxJava              : "io.reactivex.rxjava2:rxjava:2.2.2",
      rxPreferences       : "com.f2prateek.rx.preferences2:rx-preferences:2.0.0",
      timber              : "com.jakewharton.timber:timber:4.7.1",

      Kotlin              : [
          std              : "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}",
          coroutines       : "org.jetbrains.kotlinx:kotlinx-coroutines-core:${versions.coroutines}",
          coroutinesAndroid: "org.jetbrains.kotlinx:kotlinx-coroutines-android:${versions.coroutines}",
          gradlePlugin     : "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}",
      ],

      junit               : "junit:junit:4.12",
      mockito             : "org.mockito:mockito-core:2.22.0",
      mockitoKotlin       : "com.nhaarman:mockito-kotlin-kt1.1:1.6.0",
      truth               : "com.google.truth:truth:0.42",
  ]

  repositories {
    maven { url "https://maven.google.com" }
    jcenter()
    google()
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
  }

  dependencies {
    classpath deps.androidGradlePlugin
    classpath deps.Kotlin.gradlePlugin
    classpath deps.fabricGradlePlugin
  }
}

plugins {
  id 'com.gradle.build-scan' version '1.16'
  id 'com.github.ben-manes.versions' version '0.20.0'
}

buildScan { licenseAgreementUrl = 'https://gradle.com/terms-of-service'; licenseAgree = 'yes' }

allprojects {
  repositories {
    maven { url "https://maven.google.com" }
    google()
    jcenter()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
  }

  dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
      rules.all { ComponentSelection selection ->
        def candidate = selection.candidate
        if (['rc', 'beta', 'alpha'].any { forbidden -> candidate.version.toLowerCase().contains(forbidden) }) selection.reject("blacklisted")
        if (candidate.group == 'javax.annotation' && candidate.version == '1.0-20050927.133100') selection.reject()
      }
    }
  }
  configurations.all {
    resolutionStrategy {
      force deps.AndroidX.supportAnnotations
      force deps.Kotlin.std
      force 'com.google.code.findbugs:jsr305:3.0.1'
    }
  }
}

apply from: "predexDisabler.gradle"

task importStrings(type: Exec) {
  executable 'sh'
  args "-c", "tx pull -af --minimum-perc=5"
}
