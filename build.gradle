import de.ph1b.audiobook.versioning.Dependencies

buildscript {

  repositories {
    jcenter()
    google()
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath Dependencies.androidGradlePlugin
    classpath Dependencies.Kotlin.gradlePlugin
    classpath Dependencies.fabricGradlePlugin
  }
}

plugins {
  id 'com.gradle.build-scan' version '1.11'
  id 'com.github.ben-manes.versions' version '0.17.0'
}

buildScan { licenseAgreementUrl = 'https://gradle.com/terms-of-service'; licenseAgree = 'yes' }

allprojects {
  repositories {
    jcenter()
    maven { url "https://maven.google.com" }
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
  }

  dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
      rules.all { ComponentSelection selection ->
        def candidate = selection.candidate
        if (['rc', 'beta', 'alpha'].any { forbidden -> candidate.version.contains(forbidden) }) selection.reject("blacklisted")
        if (candidate.group == 'javax.annotation' && candidate.version == '1.0-20050927.133100') selection.reject()
      }
    }
  }
  configurations.all {
    resolutionStrategy {
      force Dependencies.Support.v13
      force Dependencies.Support.supportAnnotations
      force Dependencies.Kotlin.std
      force 'com.google.code.findbugs:jsr305:3.0.1'
    }
  }
}

apply from: "predexDisabler.gradle"

task importStrings(type: Exec) {
  executable 'sh'
  args "-c", "tx pull -af --minimum-perc=5"
}
