apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply from: "../artifacts.gradle"

android {

    // dynamically pass 21 to the compiler for bulid speedup without losing lint warnings.
    int minSdk = hasProperty('minSdk') ? minSdk.toInteger() : 15

    compileSdkVersion 24
    buildToolsVersion '24.0.1'

    defaultConfig {
        applicationId "de.ph1b.audiobook"
        minSdkVersion minSdk
        targetSdkVersion 24
        versionCode 127
        versionName '16.08'
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.pro'
        }
        debug {
            minifyEnabled false
            shrinkResources false
//            applicationIdSuffix ".debug"
        }
    }

    testOptions.unitTests.returnDefaultValues = true

    lintOptions.abortOnError = false

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

kapt.generateStubs = true

dependencies {
    // support library
    def supportVersion = "24.2.0"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha7'
    compile 'com.google.android.gms:play-services-auth:9.2.1'

    // image loading
    compile "com.squareup.picasso:picasso:2.5.2"

    // material styled dialogs
    compile 'com.afollestad.material-dialogs:core:0.9.0.1'

    // fab
    compile "com.getbase:floatingactionbutton:1.10.1"

    // dependency injection
    def daggerVersion = "2.6"
    compile "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"
    provided "javax.annotation:jsr250-api:1.0"

    //testing
    testCompile "junit:junit:4.12"
    testCompile "org.robolectric:robolectric:3.1"
    testCompile "org.assertj:assertj-core:1.7.1"

    // rx extensions
    compile "io.reactivex:rxandroid:1.2.1"
    compile "io.reactivex:rxjava:1.1.9"
    compile 'com.f2prateek.rx.preferences:rx-preferences:1.0.2'

    // kotlin
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // player
    compile "com.github.AntennaPod:AntennaPod-AudioPlayer:v1.0.16"

    // logs
    compile 'com.github.PaulWoitaschek:Slimber:1.0.4'

    // permissions
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.7.0@aar'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}